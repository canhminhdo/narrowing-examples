fmod IBOOL is
    sorts ITrue IFalse IBool .
    subsorts ITrue IFalse < IBool .

    op tt : -> ITrue [ctor] . 
    op ff : -> IFalse [ctor] .
endfm

fmod LOC is
    sorts RLoc WLoc NCLoc CLoc Loc .
    subsort CLoc < Loc .
    subsorts RLoc WLoc < NCLoc < Loc .

    op rs : -> RLoc [ctor] .
    op l1 : -> RLoc [ctor] .
    op l2 : -> RLoc [ctor] .
    op l3 : -> WLoc [ctor] .
    op l4 : -> WLoc [ctor] .
    op l5 : -> WLoc [ctor] .
    op l6 : -> WLoc [ctor] .
    op l7 : -> NCLoc [ctor] .
    op l8 : -> NCLoc [ctor] .
    op l9 : -> NCLoc [ctor] .
    op l10 : -> NCLoc [ctor] .
    op l11 : -> NCLoc [ctor] .
    op l12 : -> NCLoc [ctor] .
    op cs : -> CLoc [ctor] .
endfm

fmod PROC is
    pr LOC .
    pr IBOOL .
    sorts RProc WProc NCProc CProc Proc  .
    subsort CProc < Proc .
    subsorts RProc WProc < NCProc < Proc .
    
    op (pc:_, lock:_) : Loc IBool -> Proc [ctor] .
    op (pc:_, lock:_) : RLoc IBool -> RProc [ctor] .
    op (pc:_, lock:_) : WLoc IBool -> WProc [ctor] .
    op (pc:_, lock:_) : NCLoc IBool -> NCProc [ctor] .
    op (pc:_, lock:_) : CLoc IBool -> CProc [ctor] .
endfm

fmod PROCLIST is
    pr PROC .

    sorts Empty RProcList WProcList NCProcList CProcList ProcList .
    sorts NeRProcList NeWProcList NeNCProcList NeCProcList NeProcList .
    subsorts Empty < CProcList < ProcList .
    subsorts Empty < RProcList WProcList < NCProcList < ProcList .
    subsorts Proc < NeProcList < ProcList .
    subsorts RProc < NeRProcList < RProcList < ProcList .
    subsorts WProc < NeWProcList < WProcList < ProcList .
    subsorts NCProc < NeNCProcList < NCProcList < ProcList .
    subsorts CProc < NeCProcList < CProcList < ProcList .
    subsort NeCProcList < NeProcList .
    subsorts NeRProcList NeWProcList < NeNCProcList < NeProcList .
    
    op void : -> Empty [ctor] .
    op __ : Empty Empty -> Empty [ctor assoc id: void] .
    op __ : ProcList ProcList -> ProcList [ctor assoc id: void] .
    op __ : NeProcList NeProcList -> NeProcList [ctor assoc id: void] .
    op __ : RProcList RProcList -> RProcList [ctor assoc id: void] .
    op __ : NeRProcList NeRProcList -> NeRProcList [ctor assoc id: void] .
    op __ : WProcList WProcList -> WProcList [ctor assoc id: void] .
    op __ : NeWProcList NeWProcList -> NeWProcList [ctor assoc id: void] .
    op __ : NCProcList NCProcList -> NCProcList [ctor assoc id: void] .
    op __ : NeNCProcList NeNCProcList -> NeNCProcList [ctor assoc id: void] .
    op __ : CProcList CProcList -> CProcList [ctor assoc id: void] .
    op __ : NeCProcList NeCProcList -> NeCProcList [ctor assoc id: void] .
endfm

mod MCS is
    pr PROCLIST .

    sort Config . 
    op {_|_} : ProcList ProcList -> Config [ctor] .
    
    vars PL1 PL2 PL3 PL4 PL5 PL6 : ProcList .
    vars P1 P2 P3 P4 P5 P6 : Proc .
    vars RP1 RP2 RP3 RP4 : RProc .
    vars NePL1 NePL2 NePL3 NePL4 NePL5 NePL6 : ProcList .
    vars RPL1 RPL2 RPL3 : RProcList .
    vars WPL1 WPL2 WPL3 : WProcList .
    vars NCPL1 NCPL2 NCPL3 : NCProcList .
    vars CPL1 CPL2 CPL3 : CProcList .
    vars L L1 L2 : Loc .
    vars B B1 B2 : IBool .
    vars CL1 CL2 : CLoc .

    rl [rs1] : --- rs 
       (pc: rs, lock: B)
    => (pc: l1, lock: B) [narrowing] .

    rl [want] : --- nextp := nop;
       (pc: l1, lock: B)
    => (pc: l2, lock: B) [narrowing] .

    rl [stprd] : --- predep := fetch&store(glock,p);
       { PL1                   | PL2 (pc: l2, lock: B) PL3 }
    => { PL1 (pc: l3, lock: B) | PL2 PL3 } [narrowing] .

    rl [chprdThen] : --- if predep  ̸= nop -> Then l4 -> Else cs
       { NePL1 (pc: l3, lock: B) PL1 | PL2 } 
    => { NePL1 (pc: l4, lock: B) PL1 | PL2 } [narrowing] .
       
    rl [chprdElse] : --- if predep  ̸= nop -> Then l4 -> Else cs
       { (pc: l3, lock: B) PL1 | PL2 } 
    => { (pc: cs, lock: B) PL1 | PL2 }  [narrowing] .

    rl [stlck] : --- lockp := true
       (pc: l4, lock: B)
    => (pc: l5, lock: tt) [narrowing] .

    rl [stnpr] : --- nextpredep := p;
       { PL1 (pc: l5, lock: B) NePL1 | PL2 }
    => { PL1 (pc: l6, lock: B) NePL1 | PL2 } [narrowing] .

    rl [chlckThen] : --- repeat while lockp -> Then l6 -> Else cs
       (pc: l6, lock: tt)
    => (pc: l6, lock: tt) [narrowing] .

    rl [chlckElse] : --- repeat while lockp -> Then l6 -> Else cs
       (pc: l6, lock: ff)
    => (pc: cs, lock: ff) [narrowing] .

    rl [exit] : --- critical section
       (pc: cs, lock: B)
    => (pc: l7, lock: B) [narrowing] .

    rl [chknxtThen] : --- if nextp = nop -> Then l8 -> Else l11
       { PL1 (pc: l7, lock: B) | PL2 }
    => { PL1 (pc: l8, lock: B) | PL2 } [narrowing] .

    rl [chlknxtElse] : --- if nextp = nop -> Then l8 -> Else l11
       { PL1 (pc:  l7, lock: B) NePL1 | PL2 }
    => { PL1 (pc: l11, lock: B) NePL1 | PL2 } [narrowing] .

    rl [chglkThen] : --- comp&swap(glock, p, nop) then -> l9 else -> l10
       { PL1 (pc: l8, lock: B) | PL2 }
    => { PL1 (pc: l9, lock: B) | PL2 } [narrowing] .

    rl [chglkElse] : --- comp&swap(glock, p, nop) then -> l9 else -> l10
       { PL1 (pc:  l8, lock: B) NePL1 | PL2 }
    => { PL1 (pc: l10, lock: B) NePL1 | PL2 } [narrowing] .

    rl [go2rs1] : --- goto rs
       (pc: l9, lock: B)
    => (pc: rs, lock: B) [narrowing] .

    rl [rpnxThen] : --- repeat while nextp = nop -> Then l10 -> Else l11
       { PL1 (pc: l10, lock: B) | PL2 }
    => { PL1 (pc: l10, lock: B) | PL2 } [narrowing] .

    rl [rpnxElse] : --- repeat while nextp = nop -> Then l10 -> Else l11
       { PL1 (pc: l10, lock: B) NePL1 | PL2 }
    => { PL1 (pc: l11, lock: B) NePL1 | PL2 } [narrowing] .

    rl [stlnx] : --- locknextp := false
       { PL1 (pc: l11, lock: B1) (pc: L, lock: B2) PL2 | PL3 }
    => { PL1 (pc: l12, lock: B1) (pc: L, lock: ff) PL2 | PL3 } [narrowing] .

    rl [go2rs2] : --- goto rs
       (pc: l12, lock: B)
    => (pc: rs, lock: B) [narrowing] .

endm

{fold,path} vu-narrow { NCPL1 | RPL1 } 
=>* {PL1 (pc: cs, lock: B1) PL2 | PL3 } .

{fold,path} vu-narrow [1] { NCPL1 | RPL1 } 
=>* {PL1 (pc: cs, lock: B1) (pc: cs, lock: B2) PL2 | PL3 } .
eof