fmod LOC is
    sorts RsLoc WsLoc RWLoc Loc .
    subsorts RsLoc WsLoc < RWLoc < Loc .

    op rs : -> RsLoc [ctor] .
    op ws : -> WsLoc [ctor] .
    op cs : -> Loc [ctor] .
endfm

fmod PID is
    sort Pid .
endfm

fmod NAT-FVP is
    sorts Nat NzNat . 
    subsort NzNat < Nat .

    op 0 : -> Nat [ctor] .
    op 1 : -> NzNat [ctor] .
    op _+_ : NzNat Nat -> NzNat [ctor assoc comm id: 0] .
    op _+_ : Nat Nat -> Nat [ctor assoc comm id: 0] .
endfm

fmod PROC is
    pr PID .
    pr LOC .
    pr NAT-FVP .
    sorts RsProc WsProc RWProc Proc .
    subsorts RsProc WsProc < RWProc < Proc .

    op (_| pc:_, place:_) : Pid Loc Nat -> Proc [ctor] .
    op (_| pc:_) : Pid RsLoc -> RsProc [ctor] .
    op (_| pc:_, place:_) : Pid WsLoc Nat -> WsProc [ctor] .
endfm

fmod PROC-SET is
    pr PROC .
    sorts EmpProcSet RsProcSet WsProcSet RWProcSet ProcSet .
    subsort RsProc < RsProcSet .
    subsort WsProc < WsProcSet .
    subsort RWProc < RWProcSet .
    subsort Proc < ProcSet .
    subsorts EmpProcSet < RsProcSet WsProcSet < RWProcSet < ProcSet .

    op void : -> EmpProcSet [ctor] .
    op __ : ProcSet ProcSet -> ProcSet [ctor assoc comm id: void] .
    op __ : EmpProcSet EmpProcSet -> EmpProcSet [ctor ditto] .
    op __ : RsProcSet RsProcSet -> RsProcSet [ctor ditto] .
    op __ : WsProcSet WsProcSet -> WsProcSet [ctor ditto] .
    op __ : RWProcSet RWProcSet -> RWProcSet [ctor ditto] .
endfm

fmod IBOOL is
    sorts True False IBool .
    subsorts True False < IBool .

    op true : -> True [ctor] . 
    op false : -> False [ctor] .
endfm

fmod IARRAY is
    pr NAT-FVP .
    pr IBOOL .
    sorts EmpArray TElem FElem Elem TArray FArray Array .
    subsorts TElem < TArray .
    subsorts FElem < FArray .
    subsorts TElem FElem < Elem < Array .
    subsorts EmpArray < TArray FArray < Array .
    
    --- an array must has at least one false element
    op empty : -> EmpArray [ctor] . 
    op [_:_] : Nat True -> TElem [ctor] . 
    op [_:_] : Nat False -> FElem [ctor] . 
    op __ : Array Array -> Array [ctor assoc id: empty] .
    op __ : FArray FArray -> FArray [ctor ditto] .
    op __ : TArray TArray -> TArray [ctor ditto] .
    op __ : EmpArray EmpArray -> EmpArray [ctor ditto] .
endfm

mod ANDERSON is
    pr PROC-SET .
    pr IARRAY .
    sort Config . 

    op {_|`next:_|`array:_} : ProcSet Nat Array -> Config [ctor] .
  
    vars RWPSET RWPSET' : RWProcSet .
    vars RsPSET : RsProcSet .
    vars N1 N2 N3 N4 N5 : Nat .
    vars NzN1 : NzNat .
    vars PSET PSET' : ProcSet . 
    vars B1 B2 : IBool .
    vars A A' : Array .
    vars TA : TArray .
    vars FA : FArray .
    vars P P' : Pid .
    var PROC : Proc .
    vars L L' : Loc .

    --- eq [no-redundance-next] : (P | pc: L, place: N1) (P' | pc: L', place: N1) = (P | pc: L, place: N1) .
    eq [no-redundance-proc] : (P | pc: L, place: N1) (P | pc: L', place: N2) = (P | pc: L, place: N1) .
    eq [no-redundance-array] : [N1 : B1] [N1 : B2] = [N1 : B1] .

    rl [want] : 
        {(P | pc: rs) PSET | next: N1 | array: A }
    =>  {(P | pc: ws, place: N1) PSET | next: (N1 + 1) | array: A } [narrowing] .

    rl [try] : 
        {(P | pc: ws, place: N1) PSET | next: N1 + NzN1 | array: TA [N1 : true] FA }
    =>  {(P | pc: cs, place: N1) PSET | next: N1 + NzN1 | array: TA [N1 : true] FA } [narrowing] .

    rl [exit] : 
        {(P | pc: cs, place: N1) PSET | next: N1 + NzN1 | array: TA [N1 : true] [N1 + 1 : false] FA }
    =>  {(P | pc: rs) PSET | next: N1 + NzN1 | array: TA [N1 : true] [N1 + 1 : true] FA } [narrowing] .
endm

--- Proving cs is reachable
{fold,path} vu-narrow {filter,delay} [1] in ANDERSON : 
    {RsPSET | next: N1 | array: TA [N1 : true] FA} 
=>* {(P | pc: cs, place: N2) PSET | next: N2 + NzN1 | array: A} .

--- Negatively prove the mutex property
{fold,path} vu-narrow {filter,delay} [1] in ANDERSON :
    { RsPSET | next: N1 | array: [N1 : true] FA}
=>* {(P | pc: cs, place: N2) (P' | pc: cs, place: N3) PSET | next: N4 | array: A} .
